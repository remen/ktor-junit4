apply plugin: 'application'
applicationName = 'app' // For convenience in Dockerfile, we make this the same for all projects.
mainClassName = 'io.github.remen.ktorjunit4.Application'
// To speed up building a little bit, we disable building .tar.gz and .zip archives
distTar.enabled = false
distZip.enabled = false

ext.dockerTag = "meltwater-docker-owl-registry.bintray.io/petter/${project.name}:${project.version}"
ext.dockerName = "meltwater-docker-owl-registry.bintray.io/petter/${project.name}"

task("dockerBuild", type: Exec) {
    group = 'Docker'
    description = 'Builds a docker image from this project'

    inputs.files "Dockerfile"
    inputs.files tasks.installDist.outputs

    commandLine 'docker', 'build', '-t', "${dockerName}:${project.version}", '-t', "${dockerName}:latest", "."
}

task dockerPushVersion(type: Exec) {
    dependsOn("dockerBuild")
    commandLine 'docker', 'push', "${dockerName}:${project.version}"
}

task dockerPushLatest(type: Exec) {
    dependsOn("dockerBuild")
    commandLine 'docker', 'push', "${dockerName}:latest"
}

task dockerPush {
    dependsOn(dockerPushLatest, dockerPushVersion)
}

task publish {
    group = "Publishing"
    description = "Publishes build artifacts to remote repositories"

    dependsOn("check", "dockerPush")
}
